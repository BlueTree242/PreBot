plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
    id 'maven-publish'
}
repositories {
    mavenCentral()
}
var isCI = project.properties["BUILD_NUMBER"] != null // jenkins
        || System.getenv("BUILD_NUMBER") != null
        || project.properties["GIT_COMMIT"] != null // jitpack
        || System.getenv("GIT_COMMIT") != null
var branch = System.getenv("GIT_BRANCH") ?: project.project.properties["GIT_BRANCH"]
if (branch == null) branch = "unknown"
var major = 1
var minor = 0
var revision = 0
var verClassifier = 'alpha'
var versionNoCommit = "${major}.${minor}.${revision}_${verClassifier}"
var commit = System.getenv("GIT_COMMIT") ?: project.project.properties["GIT_COMMIT"]
if (isCI && commit != null)
    commit = commit.substring(0, 7)
else commit = 'LOCAL'
System.out.println("Commit: " + commit)
System.out.println("Branch: " + branch)
project.ext.set('branch', branch)
project.ext.set('major', major)
project.ext.set('minor', minor)
project.ext.set('revision', revision)
project.ext.set('verClassifier', verClassifier)
project.ext.set('commit', commit)
group = 'me.bluetree242.prebot'
allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'
    compileJava.options.encoding 'UTF-8'
    version = "${versionNoCommit}${branch != 'origin/master' ? "-${commit}" : ''}${branch != 'origin/master' ? '-SNAPSHOT' : ''}"
    shadowJar {
        classifier 'withDependencies'
    }
    build.dependsOn shadowJar
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'

        testCompileOnly 'org.projectlombok:lombok:1.18.24'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
    }
    repositories {
        mavenCentral()
    }
}
subprojects {
    task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
        dependsOn(javadoc)
        from javadoc.destinationDir
        classifier = 'javadoc'
    }
    task packageSources(type: Jar, dependsOn: 'classes') {
        from sourceSets.main.allSource
        classifier = 'sources'
    }
    publishing {
        publications {
            "$project.name"(MavenPublication) {
                groupId rootProject.group
                artifactId project.name
                version project.version
                from components.java
                artifact(packageSources)
                artifact(packageJavadoc)
                repositories {
                    maven {
                        credentials {
                            username = System.getenv('REPO_USERNAME')
                            password = System.getenv('REPO_PASSWORD')
                        }

                        url = "https://repo.bluetree242.ml/repository/maven-${branch != null && branch == 'origin/releases' ? "releases" : "snapshots"}/"
                    }
                }
            }
        }
    }
}

allprojects {
    apply plugin: 'java'
    java.sourceCompatibility = JavaVersion.VERSION_1_8
    java.targetCompatibility = JavaVersion.VERSION_1_8
}