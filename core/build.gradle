plugins {
    id 'java-library'
    id("com.github.monosoul.yadegrap") version "1.0.0"
    id("net.kyori.blossom") version "1.2.0"
}
blossom {
    var verClass = "src/main/java/me/bluetree242/prebot/api/PreBotVersion.java"
    replaceToken("@versionMajor@", major, verClass)
    replaceToken("@branch@", branch, verClass)
    replaceToken("@versionMinor@", minor, verClass)
    replaceToken("@versionRevision@", revision, verClass)
    replaceToken("@versionClassifier@", verClassifier, verClass)
    replaceToken("@commit@", commit, verClass)
    replaceToken("@version@", version, verClass)
}

jar.baseName = 'core'

dependencies {
    api("net.dv8tion:JDA:5.0.0-alpha.21")
    api('org.yaml:snakeyaml:1.33')
    api('org.slf4j:slf4j-api:2.0.3')
    api 'space.arim.dazzleconf:dazzleconf-ext-snakeyaml:1.2.1'
    api 'me.bluetree242.jdaeventer:JDAEventer:1.0.0_alpha-6aef0af-SNAPSHOT'
    api 'org.apache.commons:commons-lang3:3.12.0'
}

tasks.getByName('javadoc') {
    dependsOn tasks.getByName('delombok')
    source = tasks.getByName('delombok')
    if (JavaVersion.VERSION_1_8 < JavaVersion.current()) {
        options.addStringOption("-release", "8")
        options.addBooleanOption("html5", true) // Adds search bar
    }
    // Fix for https://stackoverflow.com/questions/52326318/maven-javadoc-search-redirects-to-undefined-url
    if (JavaVersion.current() in JavaVersion.VERSION_11..JavaVersion.VERSION_12) {
        options.addBooleanOption("-no-module-directories", true)
    }
    options.links('https://docs.oracle.com/javase/8/docs/api/', 'https://ci.dv8tion.net/job/JDA5/javadoc/', 'https://ci.bluetree242.ml/job/JDAEventer/javadoc/', 'https://www.javadoc.io/static/org.slf4j/slf4j-api/2.0.0/')
    exclude("me/bluetree242/prebot/core")
    options.docTitle("PreBot ${version} API")
    options.windowTitle("PreBot ${version} API")
}




